configurations {
    ktlint
}

def versions = [
        jacoco: '0.8.2',
]

buildscript {

    ext {
        kotlin_version = '1.3.21'
        spring_boot_version = '2.0.4.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlin_version")
        classpath("org.jetbrains.kotlin:kotlin-noarg:$kotlin_version")
        classpath('com.github.ben-manes:gradle-versions-plugin:0.20.0')
        classpath('de.undercouch:gradle-download-task:3.4.3')
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

apply plugin: 'kotlin-jpa'
apply plugin: 'kotlin-spring'
apply plugin: 'jacoco'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'de.undercouch.download'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '5.3.1'
}

jar {
    baseName = 'zally'
    version = '1.0.0'
}

dependencies {
    compile project('zally-rule-api')

    compile("io.swagger.parser.v3:swagger-parser:2.0.2")
    compile("com.github.java-json-tools:json-schema-validator:2.2.10")
    compile("org.springframework.boot:spring-boot-starter-web:$spring_boot_version")
    compile("org.springframework.boot:spring-boot-starter-undertow:$spring_boot_version")
    compile("org.springframework.boot:spring-boot-starter-actuator:$spring_boot_version")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:$spring_boot_version") {
        exclude group: "org.hibernate", module: "hibernate-entitymanager"
    }
    compile('org.flywaydb:flyway-core:5.1.4')
    compile('org.hsqldb:hsqldb:2.4.1')
    compile('org.postgresql:postgresql:42.2.4')
    compile('org.hibernate:hibernate-core:5.3.5.Final')
    compile('org.jadira.usertype:usertype.core:7.0.0.CR1') {
        exclude group: "org.hibernate", module: "hibernate-entitymanager"
    }
    compile("com.fasterxml.jackson.module:jackson-module-parameter-names")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.9+")
    compile("org.zalando.stups:stups-spring-oauth2-server:1.0.22")
    compile("org.zalando:problem-spring-web:0.23.0")
    compile("org.zalando:twintip-spring-web:1.1.0")
    compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    compile("io.github.config4k:config4k:0.4.1")

    compile("de.mpg.mpi-inf:javatools:1.1")

    testCompile project('zally-rule-api')

    testCompile("net.jadler:jadler-core:1.3.0")
    testCompile("net.jadler:jadler-jdk:1.3.0")
    testCompile("net.jadler:jadler-junit:1.3.0")
    testCompile("org.springframework.boot:spring-boot-starter-test:$spring_boot_version")
    testCompile("org.assertj:assertj-core:3.11.0")
    testCompile('com.jayway.jsonpath:json-path-assert:2.4.0')
    testCompile('org.mockito:mockito-core:2.23.4')

    ktlint("com.github.shyiko:ktlint:0.27.0")
}

jacoco {
    toolVersion = versions.jacoco
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}
check.dependsOn jacocoTestReport

task ktlint(type: JavaExec, group: "formatting") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

task ktlintIntegrateIJ(type: JavaExec, group: "formatting") {
    description = "Apply ktlint settings to IDEA-based IDE project settings."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--apply-to-idea-project", "-y"
}

bootRun {
    jvmArgs = ["-Dspring.profiles.active=dev"]
}

task downloadSwaggerSchema {
    doFirst {
        download {
            src 'http://swagger.io/v2/schema.json'
            dest "$rootDir/src/main/resources/schemas/swagger-schema.json"
        }
        download {
            src 'http://json-schema.org/draft-04/schema'
            dest "$rootDir/src/main/resources/schemas/json-schema.json"
        }
    }
}
